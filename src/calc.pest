res = { "return" ~ assign }
main = { SOI ~ stmt* ~ EOI}
stmt = _{ assign ~ semiclon | res ~ semiclon}
assign = { equation ~ (asnop ~ assign)? }
equation = { relational ~ (equalop ~ relational)* }
relational = {expr ~ (relop ~ expr)* }
expr = {factor ~ (exprop ~ factor)* }
factor = { unary ~ (factop ~ unary)* }
unary = { (addop | subop)? ~ atom }
atom = { ident | num | "(" ~ assign ~ ")" }
num = @{ASCII_DIGIT+}
ident = @{ !keywords ~ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT)* }

equalop = _{ eqop | nqop }
relop = _{ leop | ltop | geop | gtop }
exprop = _{ addop | subop }
factop = _{ mulop | divop }

semiclon = _{ ";" }
asnop = { "=" }
eqop = { "==" }
nqop = { "!=" }
ltop = { "<" }
leop = { "<=" }
gtop = { ">" }
geop = { ">=" }
addop = { "+" }
subop = { "-" }
mulop = { "*" }
divop = { "/" }
keywords = { "return" }

WHITESPACE = _{ " " | NEWLINE }
