res = { "return" ~ assign }
main = { SOI ~ stmt* ~ EOI}
stmt = _{ assign ~ semiclon | res ~ semiclon | ifstmt | block | whilestmt | forstmt }
assign = { equation ~ (asnop ~ assign)? }
equation = { relational ~ (equalop ~ relational)* }
relational = {expr ~ (relop ~ expr)* }
expr = {factor ~ (exprop ~ factor)* }
factor = { unary ~ (factop ~ unary)* }
unary = { (addop | subop)? ~ atom }
atom = { funccall | ident | num | "(" ~ assign ~ ")" }
num = @{ASCII_DIGIT+}
ident = @{ !keywords ~ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT)* }
ifstmt = { "if" ~ "(" ~ assign ~ ")" ~ stmt ~ ("else" ~ stmt)? }
block = { "{" ~ stmt* ~ "}" }
whilestmt = { "while" ~ "(" ~ assign ~ ")" ~ stmt }
forstmt = { "for" ~ forcond ~ stmt }
forcond = { "(" ~ assign? ~  forsep ~ assign? ~  forsep ~ assign? ~ ")" }
funccall = { ident ~ "(" ~ funcargs? ~ ")" }
funcargs = { assign ~ ( comma ~ assign )* }

equalop = _{ eqop | nqop }
relop = _{ leop | ltop | geop | gtop }
exprop = _{ addop | subop }
factop = _{ mulop | divop }

semiclon = _{ ";" }
forsep = { ";" }
asnop = { "=" }
eqop = { "==" }
nqop = { "!=" }
ltop = { "<" }
leop = { "<=" }
gtop = { ">" }
geop = { ">=" }
addop = { "+" }
subop = { "-" }
mulop = { "*" }
divop = { "/" }
comma = _{ "," }
keywords = { "return" | "if" | "else" | "while" | "for" }

WHITESPACE = _{ " " | NEWLINE }
