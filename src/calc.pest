res = { "return" ~ assign }
main = { SOI ~ funcdef* ~ EOI}
stmt = _{ expr | declare | res ~ semiclon | ifstmt | block | whilestmt | forstmt }
expr = { assign ~ semiclon }
declare = { typeident ~ ident ~ semiclon }
assign = { equation ~ (asnop ~ assign)? }
equation = { relational ~ (equalop ~ relational)* }
relational = {addminus ~ (relop ~ addminus)* }
addminus = {factor ~ (exprop ~ factor)* }
factor = { unary ~ (factop ~ unary)* }
unary = { ((addop | subop)? ~ atom) |  (addr | deref) ~ unary}
atom = { funccall | ident | num | "(" ~ assign ~ ")" }
num = @{ASCII_DIGIT+}
ident = @{ !keywords ~ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT)* }
typeident = @{ !keywords ~ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT)* }
ifstmt = { "if" ~ "(" ~ assign ~ ")" ~ stmt ~ ("else" ~ stmt)? }
block = { "{" ~ stmt* ~ "}" }
whilestmt = { "while" ~ "(" ~ assign ~ ")" ~ stmt }
forstmt = { "for" ~ forcond ~ stmt }
forcond = { "(" ~ assign? ~  forsep ~ assign? ~  forsep ~ assign? ~ ")" }
funccall = { ident ~ "(" ~ funcargs? ~ ")" }
funcargs = { assign ~ ( comma ~ assign )* }
funcdef = { typeident ~ ident ~ "(" ~  funcindets? ~  ")" ~ funcbody }
funcindets = { typeident ~ ident ~ ( comma ~ typeident ~ ident)* }
funcbody = { "{" ~ stmt* ~ "}" }

equalop = _{ eqop | nqop }
relop = _{ leop | ltop | geop | gtop }
exprop = _{ addop | subop }
factop = _{ mulop | divop }

semiclon = _{ ";" }
forsep = { ";" }
asnop = { "=" }
eqop = { "==" }
nqop = { "!=" }
ltop = { "<" }
leop = { "<=" }
gtop = { ">" }
geop = { ">=" }
addop = { "+" }
subop = { "-" }
mulop = { "*" }
divop = { "/" }
comma = _{ "," }
addr = { "&" }
deref = { "*" }
keywords = { "return" | "if" | "else" | "while" | "for" }

WHITESPACE = _{ " " | NEWLINE }
